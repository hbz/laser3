package de.laser.auth

import grails.gorm.DetachedCriteria
import org.codehaus.groovy.util.HashCodeHelper
import javax.persistence.Transient

/**
 * The linking between {@link User}s and {@link Role}s, ensuring permissions for users
 */
class UserRole implements Serializable, Comparable {

	private static final long serialVersionUID = 1

    //User user
    //Role role

	Date dateCreated
	Date lastUpdated

	static belongsTo = [user: User, role: Role]

	static constraints = {
		user nullable: false
		role nullable: false, validator: { Role r, UserRole ur ->
			if (ur.user?.id) {
				if (UserRole.exists(ur.user.id, r.id)) {
					return ['userRole.exists']
				}
			}
		}
		lastUpdated nullable: true
		dateCreated nullable: true
	}

	static transients = ['sortString'] // mark read-only accessor methods

	static mapping = {
		cache           true
		id 				composite: ['role', 'user']
		version 		false
		lastUpdated     column: 'ur_last_updated'
		dateCreated     column: 'ur_date_created'
	}

	/**
	 * Compares a user role by user and role equity
	 * @param other the entity to comapre with
	 * @return are the two entities equal?
	 */
	//used where???
	@Override
	boolean equals(other) {
		if (other instanceof UserRole) {
			other.user.id == user.id && other.role.id == role.id
		}
		false //fallback
	}

	/**
	 * Gets a hash code by the {@link User}'s and {@link Role}'s ids
	 * @return the hash code generated
	 */
	//used where???
    @Override
	int hashCode() {
	    int hashCode = HashCodeHelper.initHash()
        if (user) {
            hashCode = HashCodeHelper.updateHash(hashCode, user.id)
		}
		if (role) {
		    hashCode = HashCodeHelper.updateHash(hashCode, role.id)
		}
		hashCode
	}

	@Deprecated
	static UserRole get(long userId, long roleId) {
		criteriaFor(userId, roleId).get()
	}

	@Deprecated
	static boolean exists(long userId, long roleId) {
		criteriaFor(userId, roleId).count()
	}

	@Deprecated
	private static DetachedCriteria criteriaFor(long userId, long roleId) {
		UserRole.where {
			user == User.load(userId) &&
			role == Role.load(roleId)
		}
	}

	/**
	 * Grants the given role to the given user
	 * @param user the {@link User} whom the role should be granted to
	 * @param role the {@link Role} to grant
	 * @return the new link
	 */
	static UserRole create(User user, Role role, boolean flush = false) {
		UserRole.withTransaction {
			def instance = new UserRole(user: user, role: role)
			instance.save()
			instance
		}
	}

	/**
	 * Revokes the given role from the given user
	 * @param u the {@link User} whom the role should be revoked from
	 * @param r the {@link Role} to be revoked
	 * @return was the removal successful?
	 */
	static boolean remove(User u, Role r) {
		if (u != null && r != null) {
			UserRole.where { user == u && role == r }.deleteAll()
		}
	}

	/**
	 * Revoke all roles from the given user
	 * @param u the {@link User} who should lose all his privileges
	 * @return the count of database entries removed
	 */
	static int removeAll(User u) {
		u == null ? 0 : UserRole.where { user == u }.deleteAll() as int
	}

	/**
	 * Revoke the given role from every user
	 * @param r the {@link Role} to be revoked from everyone
	 * @return the count of database entries removed
	 */
	static int removeAll(Role r) {
		r == null ? 0 : UserRole.where { role == r }.deleteAll() as int
	}

	/**
	 * Gets a comparable string of this entry, used by the comparator
	 * @return the {@link User}'s display name and {@link Role}'s name
	 */
	@Transient
	String getSortString() {
		return user.display + ' ' + role.authority
	}

	/**
	 * Stringify this entry
	 * @return the user id and role id, separated by comma
	 */
	@Override
	String toString() {
		return '(' + user?.id + ',' + role?.id + ')'
	}

	/**
	 * Compared the given entry by their strings generated by {@link #getSortString}
	 * @param obj the other instance to compare to
	 * @return the comparison result (1, 0 or -1)
	 */
	@Transient
	int compareTo(obj) {
		sortString.compareTo(((UserRole) obj)?.getSortString())
	}
}
