import java.text.SimpleDateFormat

version = '3.5'
group   = 'de.laser'

// ---[ build script ]---

buildscript {
    repositories {
        mavenLocal()
        maven { url = 'https://repo.grails.org/grails/core' }
    }
    dependencies {
        classpath 'org.grails:grails-gradle-plugin:'                            + grailsGradlePluginVersion   // 2024
        classpath 'org.grails.plugins:hibernate5:'                              + gormVersion                 // 2024
        classpath 'org.grails.plugins:database-migration:'                      + databaseMigrationVersion    // 2024
        classpath 'com.bertramlabs.plugins:asset-pipeline-gradle:'              + assetPipelineVersion        // 2024
        classpath 'com.gorylenko.gradle-git-properties:gradle-git-properties:'  + gradleGitPropertiesVersion  // 2025
        classpath 'com.github.ben-manes:gradle-versions-plugin:'                + gradleVersionsPluginVersion // 2025

        // -- dependency resolving issue
        classpath 'org.grails:grails-shell:'        + grailsShellVersion     // 2024 : + org.grails.plugins:database-migration
        classpath 'org.quartz-scheduler:quartz:'    + quartzSchedulerVersion // 2024
    }
}

// ---[ applying plugins ]---

apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'org.grails.grails-web'
apply plugin: 'org.grails.grails-gsp'
apply plugin: 'com.bertramlabs.asset-pipeline'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.gorylenko.gradle-git-properties'

// ---[ plugin: database migration ! before dependencies configuration ]---

sourceSets {
    main {
        resources { srcDir 'grails-app/migrations' }
    }
}

// ---[ config ]---

repositories {
    mavenLocal()
    maven { url = 'https://repo.grails.org/grails/core' }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
        exclude group: 'org.grails', module: 'grails-shell' // - org.grails.plugins:database-migration (exclude)
    }
}

dependencies {
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-starter-tomcat'

    compileOnly 'io.micronaut:micronaut-inject-groovy'
    console 'org.grails:grails-console'

    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
//    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    implementation 'org.grails:grails-core'
    implementation 'org.grails:grails-logging'
    implementation 'org.grails:grails-plugin-databinding'
    implementation 'org.grails:grails-plugin-i18n'
    implementation 'org.grails:grails-plugin-interceptors'
    implementation 'org.grails:grails-plugin-rest'
    implementation 'org.grails:grails-plugin-services'
    implementation 'org.grails:grails-plugin-url-mappings'
    implementation 'org.grails:grails-web-boot'
    implementation 'org.grails.plugins:async'
    implementation 'org.grails.plugins:cache'
    implementation 'org.grails.plugins:events'
    implementation 'org.grails.plugins:gsp'
    implementation 'org.grails.plugins:hibernate5:' + gormVersion
    implementation 'org.grails.plugins:scaffolding'

    implementation 'io.micronaut:micronaut-http-client'

    // --------------------------------------------------

    implementation 'org.elasticsearch:elasticsearch:'                               + elasticsearchVersion // 2025
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:' + elasticsearchVersion // 2025

    implementation 'org.grails:grails-shell:'                   + grailsShellVersion            // 2024 : + org.grails.plugins:database-migration

    implementation 'org.grails.plugins:audit-logging:'          + auditLoggingVersion           // 2021
    implementation 'org.grails.plugins:database-migration:'     + databaseMigrationVersion, {   // 2024
        exclude module: 'spring-boot-cli'
    }
    implementation 'org.grails.plugins:grails-executor:'        + grailsExecutorVersion     // 2016 : [deprecated]
    implementation 'org.grails.plugins:mail:'                   + mailVersion               // 2025
    implementation 'org.grails.plugins:quartz:'                 + quartzVersion             // 2024
    implementation 'org.grails.plugins:spring-security-core:'   + springSecurityCoreVersion // 2024
    implementation 'org.grails.plugins:wkhtmltopdf:'            + wkhtmltopdfVersion        // 2018

    implementation 'org.hibernate:hibernate-core:'              + hibernateVersion  // 2023
    implementation 'org.hibernate:hibernate-ehcache:'           + hibernateVersion  // 2023 : + org.hibernate:hibernate-core:5.6.15.Final
    implementation 'org.ehcache:ehcache:'                       + ehcacheVersion    // 2022
//    implementation 'org.hibernate:hibernate-jcache:' + hibernateVersion   //        replacement for hibernate-ehcache:5.6.14.Final

    implementation 'commons-io:commons-io:'                     + commonsIoVersion            // 2025
    implementation 'com.github.albfernandez:juniversalchardet:' + juniversalchardetVersion    // 2024
    implementation 'com.vladsch.flexmark:flexmark:'                         + flexmarkVersion // 2023
    implementation 'com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:'   + flexmarkVersion // 2023
    implementation 'com.vladsch.flexmark:flexmark-ext-tables:'              + flexmarkVersion // 2023
    implementation 'io.github.gpc:asynchronous-mail:'   + asynchronousMailVersion   // 2023
    implementation 'org.apache.commons:commons-lang3:'  + commonsLang3Version       // 2024
    implementation 'org.apache.poi:poi-ooxml:'          + poiOoxmlVersion           // 2017 : [freeze] : 5.x available
    implementation 'org.codehaus.gpars:gpars:'          + gparsVersion              // 2014
    implementation 'org.codehaus.janino:janino:'        + janinoVersion             // 2024 : logback
    implementation 'org.liquibase:liquibase-core:'      + liquibaseCoreVersion      // 2023 : + org.grails.plugins:database-migration // [freeze - not 4.19.1]
    implementation 'org.quartz-scheduler:quartz:'       + quartzSchedulerVersion    // 2024

    // --------------------------------------------------

    runtimeOnly 'com.bertramlabs.plugins:asset-pipeline-gradle:' + assetPipelineVersion // 2024
    runtimeOnly 'com.bertramlabs.plugins:asset-pipeline-core:'   + assetPipelineVersion // 2024
    runtimeOnly 'com.bertramlabs.plugins:asset-pipeline-grails:' + assetPipelineVersion // 2024

    runtimeOnly 'javax.xml.bind:jaxb-api:'   + jaxbApiVersion    // 2018 : [freeze]

    runtimeOnly 'org.apache.tomcat:tomcat-jdbc'
    runtimeOnly 'org.graalvm.sdk:graal-sdk:' + graalSdkVersion   // + com.bertramlabs.plugins:asset-pipeline-core [freeze]
    runtimeOnly 'org.postgresql:postgresql:' + postgresqlVersion // 2025
}

java {
    sourceCompatibility = JavaVersion.toVersion( javaVersion )
    targetCompatibility = JavaVersion.toVersion( javaVersion )
}

springBoot {
    mainClass = 'de.laser.Application'
}

bootRun {
    ignoreExitValue = true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always', 
        '-noverify', 
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

// ---[ plugin: gradle-git-properties ]---

gitProperties {
    dateFormat = "dd-MM-yyyy'T'HH:mm:ssZ"
    //dateFormatTimeZone = "PST"
    gitPropertiesDir = new File("${project.rootDir}/")
}

// ---[ war ]---

tasks.register('war.updateBuildInfo', Task) {
    group = 'laser'

    String rootDir = project.rootDir
    String buildDir = project.buildDir

    doFirst {
        Properties properties = new Properties()
        properties.setProperty('info.app.build.date', new SimpleDateFormat('dd.MM.yyyy HH:mm').format(new Date()))
        properties.setProperty('info.app.build.host', InetAddress.localHost.hostName)
        properties.setProperty('info.app.build.javaVersion', System.getProperty('java.version'))

        try {
            File gitProperties = new File("${rootDir}/git.properties")
            gitProperties.withInputStream { properties.load(it) } // read props from file

            File grailsBuildInfo = new File("${buildDir}/resources/main/META-INF/grails.build.info")
            grailsBuildInfo.withInputStream { properties.load(it) } // read props from file

            grailsBuildInfo.withOutputStream { properties.store(it, null) } // write back to file
        }
        catch (Exception e) {
        }
    }
}

war {
    enabled = true
    archiveBaseName = 'laser3'
    archiveFileName = 'laser3.war'
    duplicatesStrategy = DuplicatesStrategy.WARN
    rootSpec.exclude(
            // '**/h2-*.jar',
            '**/groovy-groovydoc-*.jar'
    )
    dependsOn compileGroovyPages
    dependsOn 'war.updateBuildInfo'
}

// ---[ plugin: asset pipeline ]---

assets {
    minifyJs = true
    minifyCss = true
    minifyOptions = [
            languageMode:   'ES6',
            targetLanguage: 'ES6',
            optimizationLevel: 'SIMPLE',

            // workaround: js compiler error
            excludes: [
                    'swagger-ui/swagger-ui-bundle.js',
                    'swagger-ui/swagger-ui-standalone-preset.js',
            ]
    ]
    enableSourceMaps = false
    skipNonDigests = true
    maxThreads = 8

    from '/themes'

    // workaround: js compiler error
    includes = [
            'swagger-ui/swagger-ui-bundle.js',
            'swagger-ui/swagger-ui-standalone-preset.js',
    ]

    excludes = [
            '**/*.map',
            // images/*
            // javascripts/*
            'libs/**',
            'modules/**',
            // themes/*
            'accessibility/**',
            'javascript/**',
            'laser/**',
            // vendor/*
            'crypto-js-4.1.1/**',
            'echarts/**',
            'spring-websocket/**', // tmp deactivated
            'swagger-ui/**'
    ]
}

// ---[ gradle tasks ]---

apply from: 'laserTasks.gradle'

tasks.withType(GroovyCompile).configureEach {
    configure(groovyOptions) { forkOptions.jvmArgs = ['-Xmx1024m'] }
}
