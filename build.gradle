
version '3.0.0.alpha'
group 'de.laser'

// ---[ build script

buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://repo.grails.org/grails/core' }
    }
    // https://github.com/n0mer/gradle-git-properties/issues/195
    configurations.all {
        resolutionStrategy.force "org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r"
    }
    dependencies {
        classpath 'org.grails:grails-gradle-plugin:' + grailsVersion
        classpath 'org.grails.plugins:hibernate5:7.0.5'

        // https://github.com/grails/grails-database-migration
        classpath 'org.grails.plugins:database-migration:3.1.0'                                     // from 3.0.3
        // https://github.com/bertramdev/asset-pipeline
        classpath 'com.bertramlabs.plugins:asset-pipeline-gradle:3.4.3'                             // from 3.2.4
        // https://github.com/n0mer/gradle-git-properties
        classpath 'gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.2.4'   // 2.4.0 available

        // classpath 'gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.0'    // -- nn --

        // -- dependency resolving issue

        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
        classpath 'org.quartz-scheduler:quartz:2.3.2'
    }
}

// ---[ applying plugins

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'org.grails.grails-web'
apply plugin: 'org.grails.grails-gsp'
apply plugin: 'org.grails.grails-doc'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'com.bertramlabs.asset-pipeline'
// apply plugin: 'com.github.erdi.webdriver-binaries'        // -- nn --

// ---[ plugin: database migration ! before dependencies configuration

sourceSets {
    main {
        resources {
            srcDir 'grails-app/migrations'
        }
    }
}

// ---[ config

repositories {
    mavenLocal()
    maven { url 'https://repo.grails.org/grails/core' }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    compile 'org.springframework.boot:spring-boot-starter-logging'
    compile 'org.springframework.boot:spring-boot-autoconfigure'
    compile 'org.grails:grails-core'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    provided 'org.springframework.boot:spring-boot-starter-tomcat'          // from compile
    compile 'org.grails:grails-web-boot'
    compile 'org.grails:grails-logging'
    compile 'org.grails:grails-plugin-rest'
    compile 'org.grails:grails-plugin-databinding'
    compile 'org.grails:grails-plugin-i18n'
    compile 'org.grails:grails-plugin-services'
    compile 'org.grails:grails-plugin-url-mappings'
    compile 'org.grails:grails-plugin-interceptors'
    compile 'org.grails.plugins:cache'
    compile 'org.grails.plugins:async'
    compile 'org.grails.plugins:scaffolding'
    compile 'org.grails.plugins:events'
    compile 'org.grails.plugins:hibernate5'
    compile 'org.hibernate:hibernate-core:5.4.33.Final'                     // from 5.4.18.Final
    compile 'org.grails.plugins:gsp'

    compileOnly 'io.micronaut:micronaut-inject-groovy'
    console 'org.grails:grails-console'
    profile 'org.grails.profiles:web'
    runtime 'org.apache.tomcat:tomcat-jdbc'
    runtime 'javax.xml.bind:jaxb-api:2.3.1'
//    runtime 'org.glassfish.web:el-impl:2.1.2-b03'                         // -- nn --
//    runtime 'com.h2database:h2'                                           // -- nn --
//    runtime 'com.bertramlabs.plugins:asset-pipeline-grails:3.4.3'         // from 3.2.4 // -- nn --
//    testCompile "io.micronaut:micronaut-inject-groovy"                    // -- nn --
//    testCompile "org.grails:grails-gorm-testing-support"                  // -- nn --
//    testCompile "org.mockito:mockito-core"                                // -- nn --
//    testCompile "org.grails:grails-web-testing-support"                   // -- nn --
//    testCompile "org.grails.plugins:geb"                                  // -- nn --
//    testCompile "org.seleniumhq.selenium:selenium-remote-driver:3.14.0"   // -- nn --
//    testCompile "org.seleniumhq.selenium:selenium-api:3.14.0"             // -- nn --
//    testCompile "org.seleniumhq.selenium:selenium-support:3.14.0"         // -- nn --
//    testRuntime "org.seleniumhq.selenium:selenium-chrome-driver:3.14.0"   // -- nn --
//    testRuntime "org.seleniumhq.selenium:selenium-firefox-driver:3.14.0"  // -- nn --

    // --------------------------------------------------

    // https://mvnrepository.com/artifact/org.elasticsearch.client/elasticsearch-rest-high-level-client
    compile 'org.elasticsearch:elasticsearch:7.4.2'
    compile 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.4.2'

    // https://grails-plugins.github.io/grails-spring-security-core/
    compile 'org.grails.plugins:spring-security-core:4.0.4'             // from 3.2.3
    // https://github.com/bobbywarner/grails-http-builder-helper
    compile 'org.grails.plugins:http-builder-helper:1.1.0'
    // https://github.com/symentis/grails-audit-logging-plugin/
    compile 'org.grails.plugins:audit-logging:4.0.3'                    // from 3.0.3
    // https://github.com/grails/grails-database-migration
    compile 'org.grails.plugins:database-migration:3.1.0'               // from 3.0.3
    // https://github.com/uberall/grails-executor
    compile 'org.grails.plugins:grails-executor:0.4'                    // DEPRECATED
    // https://github.com/grails3-plugins/mail
    compile 'org.grails.plugins:mail:3.0.0'                             // from 2.0.0
    // https://github.com/grails-plugins/grails-quartz
    compile 'org.grails.plugins:quartz:2.0.13'
    // https://github.com/zyro23/grails-spring-websocket
    compile "org.grails.plugins:grails-spring-websocket:2.5.0.RC1"
    // https://github.com/rlovtangen/wkhtmltopdf
    compile 'org.grails.plugins:wkhtmltopdf:1.0.0.RC9'

    // https://mvnrepository.com/artifact/org.hibernate/hibernate-ehcache
    compile 'org.hibernate:hibernate-ehcache:5.4.33.Final'    // from 5.1.16.Final
    // https://mvnrepository.com/artifact/net.sf.ehcache/ehcache
    compile 'net.sf.ehcache:ehcache:2.10.9.2'                 // from 2.10.6
//    compile 'org.hibernate:hibernate-jcache:5.4.33.Final'       // instead of hibernate-ehcache:5.1.16.Final
//    compile 'org.ehcache:ehcache:3.9.9'                         // instead of net.sf.ehcache:ehcache:2.10.9.2

    // -- apache stuff

    compile 'commons-io:commons-io:2.11.0'                      // from 2.6
    compile 'commons-fileupload:commons-fileupload:1.4'         // from 1.3.3
    compile 'org.apache.commons:commons-exec:1.3'
    compile 'org.apache.commons:commons-lang3:3.12.0'           // from 3.9
    compile 'org.apache.httpcomponents:httpmime:4.5.13'         // from 4.5.4
    compile 'org.apache.poi:poi-ooxml:3.17'                     // from 3.9 : 5.x available

    // -- others

    compile 'com.github.albfernandez:juniversalchardet:2.4.0'   // from 2.3.0
    compile 'com.opencsv:opencsv:4.6'                           // from 3.7 : 5.x available
    compile 'gov.loc:bagit:4.12.3'                              // from 4.0 : 5.x available
    compile 'org.codehaus.gpars:gpars:1.2.1'
    compile 'org.liquibase:liquibase-core:3.10.1'                // from 3.5.5 : + org.grails.plugins:database-migration

    runtime 'org.postgresql:postgresql:42.3.3'                  // from 42.3.0

    // -- dependency resolving issue

    implementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    implementation 'org.quartz-scheduler:quartz:2.3.2'
}

//webdriverBinaries {           // -- nn --
//    chromedriver '2.45.0'
//    geckodriver '0.24.0'
//}

springBoot {
    mainClassName = 'de.laser.Application'
}

bootRun {
    ignoreExitValue true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always', 
        '-noverify', 
        '-XX:TieredStopAtLevel=1',
//        '-XX:HotswapAgent=fatjar',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

// ---[ plugin: gradle-git-properties

gitProperties {
    dateFormat = "dd-MM-yyyy'T'HH:mm:ssZ"
    //dateFormatTimeZone = "PST"
    gitPropertiesDir = new File("${project.rootDir}/")
}

buildProperties {
    inputs.property('info.app.build.date', new Date().format('dd.MM.yyyy HH:mm'))
    inputs.property('info.app.build.host', InetAddress.localHost.hostName)
    inputs.property('info.app.build.javaVersion', System.getProperty('java.version'))

    doLast {
        File grailsBuildInfo = it.outputs.files.files.find { it.name == 'grails.build.info' } as File
        if (! grailsBuildInfo) return

        Properties properties = new Properties()
        grailsBuildInfo.withInputStream { properties.load(it) } // read props from file

        File BuildInfoFile = new File("${project.rootDir}/git.properties")
        BuildInfoFile.withInputStream { properties.load(it) } // read props from file

        properties.setProperty('build.time', new Date().format("dd.MM.yyyy HH:mm:ss"))

        grailsBuildInfo.withOutputStream { properties.store(it, null) } // write back to file
    }
}

// ---[ war

war {
    baseName = 'laser3'
    archiveName = 'laser3.war'
}
grails {
    pathingJar = true // windows - command line limitation
}

// ---[ plugin: asset pipeline

assets {
    minifyJs = true
    minifyCss = true
    minifyOptions = [
            languageMode: 'ES6',
            targetLanguage: 'ES5_STRICT'
    ]
    skipNonDigests = true

    from '/themes'
    excludes = [
            // javascripts/*
            'libs/**',
            'modules/**',
            // themes/*
            'accessibility/**',
            'javascript/**',
            'laser/**',
            // vendor/*
            'bulma/**',
            'cryptoJS-v3.1.2/**',
            'echarts/**',
            'datatables/**',
            'select2/**/*.js',
            'select2/**/*.css',
            'spring-websocket/**',
            'swagger-ui/**',
    ]
}

// ---[ gradle tasks

apply from: 'custom.gradle'

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

//tasks.withType(Test) {        // -- nn --
//    systemProperty 'geb.env', System.getProperty('geb.env')
//    systemProperty 'geb.build.reportsDir', reporting.file('geb/integrationTest')
//    systemProperty 'webdriver.chrome.driver', System.getProperty('webdriver.chrome.driver')
//    systemProperty 'webdriver.gecko.driver', System.getProperty('webdriver.gecko.driver')
//}
