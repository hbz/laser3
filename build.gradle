import java.text.SimpleDateFormat

version '3.0.7'
group 'de.laser'

// ---[ build script ]---

buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://repo.grails.org/grails/core' }
    }
    dependencies {
        classpath 'org.grails:grails-gradle-plugin:' + grailsGradlePluginVersion
        classpath 'org.grails.plugins:hibernate5:7.3.1'                                     // 2022
        classpath 'org.grails.plugins:database-migration:4.2.0'                             // 2023
        classpath 'com.bertramlabs.plugins:asset-pipeline-gradle:4.2.0'                     // 2023
        classpath 'com.gorylenko.gradle-git-properties:gradle-git-properties:2.4.1'         // 2022
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.46.0'                      // 2023

        // -- dependency resolving issue

        classpath 'org.quartz-scheduler:quartz:2.3.2'                                       // 2019
    }
}

// ---[ applying plugins ]---

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'org.grails.grails-web'
apply plugin: 'org.grails.grails-gsp'
apply plugin: 'com.bertramlabs.asset-pipeline'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.gorylenko.gradle-git-properties'

// ---[ plugin: database migration ! before dependencies configuration ]---

sourceSets {
    main {
        resources { srcDir 'grails-app/migrations' }
    }
}

// ---[ config ]---

repositories {
    mavenLocal()
    maven { url 'https://repo.grails.org/grails/core' }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}
configurations {
    all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.name in ['asset-pipeline-core', 'asset-pipeline-grails']) {
                // override dependency from org.grails.plugins:cache
                details.useVersion '4.2.0' // upgrade from 3.x
            }
        }
    }
}

dependencies {
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-starter-tomcat'    // implementation

    compileOnly 'io.micronaut:micronaut-inject-groovy'
    console 'org.grails:grails-console'

    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.grails:grails-core'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.grails:grails-web-boot'
    implementation 'org.grails:grails-logging'
    implementation 'org.grails:grails-plugin-rest'
    implementation 'org.grails:grails-plugin-databinding'
    implementation 'org.grails:grails-plugin-i18n'
    implementation 'org.grails:grails-plugin-services'
    implementation 'org.grails:grails-plugin-url-mappings'
    implementation 'org.grails:grails-plugin-interceptors'
    implementation 'org.grails.plugins:cache'
    implementation 'org.grails.plugins:async'
    implementation 'org.grails.plugins:scaffolding'
    implementation 'org.grails.plugins:events'
    implementation 'org.grails.plugins:hibernate5:7.3.1'    // [fixed - tmp]
    implementation 'org.grails.plugins:gsp'

    implementation 'io.micronaut:micronaut-http-client'
//    implementation 'org.springframework:spring-websocket'     // tmp deactivated
//    implementation 'org.springframework:spring-messaging'     // tmp deactivated

    profile 'org.grails.profiles:web'

    // --------------------------------------------------

    implementation 'org.elasticsearch:elasticsearch:7.16.3'                                 // 2022
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.16.3'   // 2022

    implementation 'org.grails.plugins:spring-security-core:5.2.1'          // 2023
    implementation 'org.grails.plugins:audit-logging:4.0.3'                 // 2021
    implementation 'org.grails.plugins:database-migration:4.2.0', {         // 2023
        exclude module: 'spring-boot-cli'
    }
    implementation 'org.grails.plugins:grails-executor:0.4'                 // 2016 : [deprecated]
    implementation 'org.grails.plugins:mail:3.0.0'                          // 2021 : upgrade from 2.0.0
    implementation 'org.grails.plugins:quartz:2.0.13'                       // 2019
    implementation 'org.grails.plugins:wkhtmltopdf:1.0.0.RC9'               // 2018

    implementation 'org.hibernate:hibernate-core:5.6.15.Final'              // 2023
    implementation 'org.hibernate:hibernate-ehcache:5.6.15.Final'           // 2023 : + org.hibernate:hibernate-core:5.6.15.Final
    implementation 'net.sf.ehcache:ehcache:2.10.9.2'                        // 2021
//    implementation 'org.hibernate:hibernate-jcache:5.4.33.Final'          //        replacement for hibernate-ehcache:5.6.14.Final
//    implementation 'org.ehcache:ehcache:3.9.9'                            //        replacement for net.sf.ehcache:ehcache:2.10.9.2

    implementation 'commons-io:commons-io:2.11.0'                           // 2021
    implementation 'com.github.albfernandez:juniversalchardet:2.4.0'        // 2020
    implementation 'org.apache.commons:commons-lang3:3.12.0'                // 2021
    implementation 'org.apache.poi:poi-ooxml:3.17'                          // 2017 : [freeze] : 5.x available
    implementation 'org.codehaus.gpars:gpars:1.2.1'                         // 2014
    implementation 'org.codehaus.janino:janino:3.1.9'                       // 2022 : logback
    implementation 'org.liquibase:liquibase-core:4.19.0'                    // 2023 : + org.grails.plugins:database-migration
    implementation 'org.quartz-scheduler:quartz:2.3.2'                      // 2019

    // --------------------------------------------------

    runtimeOnly 'org.apache.tomcat:tomcat-jdbc'
    runtimeOnly 'javax.xml.bind:jaxb-api:2.3.1'                             // 2018 : [?]
    runtimeOnly 'org.postgresql:postgresql:42.6.0'                          // 2023
}

springBoot {
    mainClass = 'de.laser.Application'
}

bootRun {
    ignoreExitValue true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always', 
        '-noverify', 
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

// ---[ plugin: gradle-git-properties ]---

gitProperties {
    dateFormat = "dd-MM-yyyy'T'HH:mm:ssZ"
    //dateFormatTimeZone = "PST"
    gitPropertiesDir = new File("${project.rootDir}/")
}

buildProperties {
    inputs.property('info.app.build.date', new SimpleDateFormat('dd.MM.yyyy HH:mm').format(new Date()))
    inputs.property('info.app.build.host', InetAddress.localHost.hostName)
    inputs.property('info.app.build.javaVersion', System.getProperty('java.version'))

    doLast {
        File grailsBuildInfo = it.outputs.files.files.find { it.name == 'grails.build.info' } as File
        if (! grailsBuildInfo) return

        Properties properties = new Properties()
        grailsBuildInfo.withInputStream { properties.load(it) } // read props from file
        File BuildInfoFile = new File("${project.rootDir}/git.properties")
        BuildInfoFile.withInputStream { properties.load(it) } // read props from file

        properties.setProperty('build.time', new SimpleDateFormat("dd.MM.yyyy HH:mm:ss").format(new Date()))

        grailsBuildInfo.withOutputStream { properties.store(it, null) } // write back to file
    }
}

// ---[ war ]---

war {
    enabled = true
    baseName = 'laser3'
    archiveFileName = 'laser3.war'
    duplicatesStrategy = DuplicatesStrategy.WARN
    rootSpec.exclude(
            // '**/h2-*.jar',
            '**/groovy-groovydoc-*.jar'
    )
}

// ---[ plugin: asset pipeline ]---

assets {
    minifyJs = true
    minifyCss = true
    minifyOptions = [
            languageMode:   'ES6',
            targetLanguage: 'ES5_STRICT'
    ]
    skipNonDigests = true

    from '/themes'
    excludes = [
            // javascripts/*
            'libs/**',
            'modules/**',
            // themes/*
            'accessibility/**',
            'javascript/**',
            'laser/**',
            // vendor/*
            'bulma/**',
            'crypto-js-4.1.1/**',
            'echarts/**',
            // 'spring-websocket/**', // tmp deactivated
            'swagger-ui/**',
    ]
}

// ---[ gradle tasks ]---

apply from: 'laserTasks.gradle'

tasks.withType(GroovyCompile) {
    configure(groovyOptions) { forkOptions.jvmArgs = ['-Xmx1024m'] }
}
