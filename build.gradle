import java.text.SimpleDateFormat

version '3.0.0'
group 'de.laser'

// ---[ build script ]---

buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://repo.grails.org/grails/core' }
    }
    dependencies {
        classpath 'org.grails:grails-gradle-plugin:' + grailsGradlePluginVersion
        classpath 'org.grails.plugins:hibernate5:7.3.0'                                             // from 7.0.5
        classpath 'org.grails.plugins:database-migration:4.1.0'                                     // from 3.0.3
        classpath 'com.bertramlabs.plugins:asset-pipeline-gradle:3.4.7'                             // from 3.2.4
        classpath 'com.gorylenko.gradle-git-properties:gradle-git-properties:2.4.1'                 // from 2.2.4
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.43.0'

        // -- dependency resolving issue

        classpath 'org.quartz-scheduler:quartz:2.3.2'
    }
}

// ---[ applying plugins ]---

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'org.grails.grails-web'
apply plugin: 'org.grails.grails-gsp'
apply plugin: 'com.bertramlabs.asset-pipeline'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.gorylenko.gradle-git-properties'

// ---[ plugin: database migration ! before dependencies configuration ]---

sourceSets {
    main {
        resources { srcDir 'grails-app/migrations' }
    }
}

// ---[ config ]---

repositories {
    mavenLocal()
    maven { url 'https://repo.grails.org/grails/core' }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}
configurations {
    all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.name in ['logback-classic', 'logback-core']) {
                // override dependency from org.springframework.boot:spring-boot-starter-logging
                details.useVersion '1.2.8' // downgrade from 1.2.10
            }
        }
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.name in ['asset-pipeline-core', 'asset-pipeline-grails']) {
                // override dependency from org.grails.plugins:cache
                details.useVersion '3.4.7' // upgrade from 3.0.7
            }
        }
    }
}

dependencies {
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    developmentOnly 'org.springframework.boot:spring-boot-starter-tomcat'    // implementation

    compileOnly 'io.micronaut:micronaut-inject-groovy'
    console 'org.grails:grails-console'

    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.grails:grails-core'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.grails:grails-web-boot'
    implementation 'org.grails:grails-logging'
    implementation 'org.grails:grails-plugin-rest'
    implementation 'org.grails:grails-plugin-databinding'
    implementation 'org.grails:grails-plugin-i18n'
    implementation 'org.grails:grails-plugin-services'
    implementation 'org.grails:grails-plugin-url-mappings'
    implementation 'org.grails:grails-plugin-interceptors'
    implementation 'org.grails.plugins:cache'
    implementation 'org.grails.plugins:async'
    implementation 'org.grails.plugins:scaffolding'
    implementation 'org.grails.plugins:events'
    implementation 'org.grails.plugins:hibernate5'
    implementation 'org.grails.plugins:gsp'

    implementation 'io.micronaut:micronaut-http-client'

    profile 'org.grails.profiles:web'

    // --------------------------------------------------

    implementation 'org.elasticsearch:elasticsearch:7.15.1'                                 // from 7.4.2
    implementation 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.15.1'   // from 7.4.2

    implementation 'org.grails.plugins:spring-security-core:5.1.0'          // from 3.2.3
    implementation 'org.grails.plugins:audit-logging:4.0.3'                 // from 3.0.3
    implementation 'org.grails.plugins:database-migration:4.1.0', {         // from 3.0.3
        exclude module: 'spring-boot-cli'
    }
    implementation 'org.grails.plugins:grails-executor:0.4'                 // 2016 - DEPRECATED
    implementation 'org.grails.plugins:mail:3.0.0'                          // from 2.0.0
    implementation 'org.grails.plugins:quartz:2.0.13'                       // 2019
    implementation 'org.grails.plugins:grails-spring-websocket:2.5.0.RC1'   // 2019
    implementation 'org.grails.plugins:wkhtmltopdf:1.0.0.RC9'               // 2018

    implementation 'org.hibernate:hibernate-core:5.6.9.Final'               // from 5.4.18.Final
    implementation 'org.hibernate:hibernate-ehcache:5.6.9.Final'            // from 5.1.16.Final : + org.hibernate:hibernate-core:5.6.9.Final
    implementation 'net.sf.ehcache:ehcache:2.10.9.2'                        // from 2.10.6
//    implementation 'org.hibernate:hibernate-jcache:5.4.33.Final'          // replacement for hibernate-ehcache:5.1.16.Final
//    implementation 'org.ehcache:ehcache:3.9.9'                            // replacement for net.sf.ehcache:ehcache:2.10.9.2

    implementation 'commons-io:commons-io:2.11.0'                           // from 2.6
    implementation 'com.github.albfernandez:juniversalchardet:2.4.0'        // from 2.3.0
    implementation 'org.apache.commons:commons-lang3:3.12.0'                // from 3.9
    implementation 'org.apache.poi:poi-ooxml:3.17'                          // from 3.9 : 5.x available
    implementation 'org.codehaus.gpars:gpars:1.2.1'                         // 2014
    implementation 'org.liquibase:liquibase-core:4.11.0'                    // from 3.5.5 : + org.grails.plugins:database-migration
    implementation 'org.quartz-scheduler:quartz:2.3.2'                      // 2019

    // --------------------------------------------------

    runtimeOnly 'org.apache.tomcat:tomcat-jdbc'
    runtimeOnly 'javax.xml.bind:jaxb-api:2.3.1'
    runtimeOnly 'org.postgresql:postgresql:42.5.0'                          // from 42.3.0
}

springBoot {
    mainClass = 'de.laser.Application'
}

bootRun {
    ignoreExitValue true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always', 
        '-noverify', 
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

// ---[ plugin: gradle-git-properties ]---

gitProperties {
    dateFormat = "dd-MM-yyyy'T'HH:mm:ssZ"
    //dateFormatTimeZone = "PST"
    gitPropertiesDir = new File("${project.rootDir}/")
}

buildProperties {
    inputs.property('info.app.build.date', new SimpleDateFormat('dd.MM.yyyy HH:mm').format(new Date()))
    inputs.property('info.app.build.host', InetAddress.localHost.hostName)
    inputs.property('info.app.build.javaVersion', System.getProperty('java.version'))

    doLast {
        File grailsBuildInfo = it.outputs.files.files.find { it.name == 'grails.build.info' } as File
        if (! grailsBuildInfo) return

        Properties properties = new Properties()
        grailsBuildInfo.withInputStream { properties.load(it) } // read props from file
        File BuildInfoFile = new File("${project.rootDir}/git.properties")
        BuildInfoFile.withInputStream { properties.load(it) } // read props from file

        properties.setProperty('build.time', new SimpleDateFormat("dd.MM.yyyy HH:mm:ss").format(new Date()))

        grailsBuildInfo.withOutputStream { properties.store(it, null) } // write back to file
    }
}

// ---[ war ]---

war {
    enabled = true
    baseName = 'laser3'
    archiveFileName = 'laser3.war'
    duplicatesStrategy = DuplicatesStrategy.WARN
    rootSpec.exclude(
            // '**/h2-*.jar',
            '**/groovy-groovydoc-*.jar'
    )
}

// ---[ plugin: asset pipeline ]---

assets {
    minifyJs = true
    minifyCss = true
    minifyOptions = [
            languageMode:   'ES6',
            targetLanguage: 'ES5_STRICT'
    ]
    skipNonDigests = true

    from '/themes'
    excludes = [
            // javascripts/*
            'libs/**',
            'modules/**',
            // themes/*
            'accessibility/**',
            'javascript/**',
            'laser/**',
            // vendor/*
            'bulma/**',
            'crypto-js-4.1.1/**',
            'echarts/**',
            'select2/**/*.js',
            'select2/**/*.css',
            'spring-websocket/**',
            'swagger-ui/**',
    ]
}

// ---[ gradle tasks ]---

apply from: 'laserTasks.gradle'

tasks.withType(GroovyCompile) {
    configure(groovyOptions) { forkOptions.jvmArgs = ['-Xmx1024m'] }
}
